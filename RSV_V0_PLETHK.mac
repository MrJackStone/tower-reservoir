! RSV_V0_PLETALL, etName, stressKey, type, imgName
! RSV_V0_PLETALL,   arg1,      arg2, arg3,    arg4

! arg1: etName     -  name of element table to plot (used only for arg2=0 or arg2=1)
! arg2: stressKey  -  string indicating which stress to plot:
!                        ['SEQV'] von Mises stress
!                        ['SX']   sigma X axial stress (ESYS)
!                        ['SY']   sigma Y axial stress (ESYS)
! arg3: type       -  Stress display option:
!                        [0] display stresses
!                        [1] display bicolor approved/rejected
!                        [2] display design factor (1/SF)
! arg4: imgName    -  name of the image to be saved


! Set window display
*if,arg3,eq,2,then
    digits=3
*else
    digits=1
*endif
wnlayout,2,1,1
wnpost,digits
*if,arg3,eq,1,then
    /udoc,all,cntr,off
*endif

! Create labels for stress being plotted
stressKey=upcase(arg2)
*if,stressKey,eq,'SEQV',then
    stressName='Von Mises'
    dfETprefix='DFSQ_T'
*elseif,stressKey,eq,'SX',then
    stressName='Sigma X'
    dfETprefix='DFSX_T'
*elseif,stressKey,eq,'SY',then
    stressName='Sigma Y'
    dfETprefix='DFSY_T'
*endif

! Create window titles
*if,arg3,eq,0,then
    ! Stress plot:
    topTitle=strcat(stressName,' stress [MPa]')
    botTitle='Envelope maximums'
*elseif,arg3,eq,1,then
    ! Approval plot:
    topTitle=strcat('Criterion: ',strcat(stressName,' stress'))
    botTitle='BLUE: approved | RED: failed'
*elseif,arg3,eq,2,then
    ! Design factor plot:
    topTitle=strcat('Criterion: ',strcat(stressName,' stress'))
    botTitle='Design Factor: OK<1, FAILS>1'
*endif
/annot,dele
wntitle,'Front view',1
wntitle,'Back view',2
wntitle,topTitle,4
wntitle,botTitle,3

! Set contour limits
*if,arg3,eq,2,then
    RSV_V0_THKETENV,'cntr_min','cntr_max',dfETprefix
    clims,cntr_min,cntr_max
*else
    clims
*endif

lsel,s,line,,all
cm,COMPL_temp,line
/color,cm,dgra,COMPL_temp

go2png,1200

    ! Background: lines in grey
    lplot
    /noerase
    cmdele,COMPL_temp

    ! Plot disregarded elements in deformed configuration
    cmsel,s,COMPE_disregarded
    cmsel,s,COMPA_Roof
    esla,a
    cm,COMPE_temp,elem
    /color,cm,dgra,COMPE_temp
    /gline,all,-1
    nsle,s
    pldisp
    cmdele,COMPE_temp
    /gline,all,0

    *do,nthThk,1,SEC_PlateCount,1

        ! Get element table to plot
        *if,arg3,eq,2,then
            ! Design factor plot:
            etname=strcat(dfETprefix,chrval(nthThk))
        *else
            ! Stress or approval plot:
            etname=arg1
        *endif

        ! Select plate elements by thickness (cross-section)
        cmsel,s,COMPA_Tube
        esla,s
        esel,r,sec,,nthThk
        cmsel,u,COMPE_disregarded
        nsle,s
        *get,ec,elem,,count

        *if,ec,gt,0,then

            ! Set contour limits
            *if,arg3,eq,1,then
                ! Bicolor plot
                fadm=MAT_STEEL_fadm(nthThk)
                clims,(fadm-10)*1e-6,(fadm+10)*1e-6
            *endif

            ! Plot element table
            pletab,%etname%

        *endif
    *enddo

endpng,%arg4%

/erase
/annot,dele
clims
view1w
windefa,1
